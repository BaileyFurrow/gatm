\documentclass[../gatm.tex]{subfiles}

\begin{document}

\section{Rotation and Reflection Groups}

\begin{asydef}
pair unitify(pair p) {
return p / (sqrt(p.x * p.x + p.y * p.y));
}

pair cis(real angle) {
return (cos(angle), sin(angle));
}

void drawTriangle(pair offset, real v_stretch, string[] order, bool showaxes = false, bool[] shownaxes = {true, true, true}, bool labelaxes = true) {
pair A = offset + (sqrt(3), 0);
pair B = offset + (0, 1);
pair C = offset + (0, -1);
pair O = offset + (sqrt(3) / 3, 0);

draw(A--B--C--cycle);

real egg = v_stretch - 1;

label(order[0], (O + egg * A) / v_stretch);
label(order[1], (O + egg * B) / v_stretch);
label(order[2], (O + egg * C) / v_stretch);

void drawExtLine(pair A, pair B, real extra = 0.2, string labelstr = "") {
	pair C = (A + extra * (A - B)), D = (B + extra * (B - A));
	draw(C -- D, dashed);
	label(labelstr, C, unitify(A - B));
}

if (showaxes) {
pair[] vertices = {A, B, C};
string[] axisnames = {"$A\,(f)$", "$B\,(f_B)$", "$C\,(f_C)$"};

for (int i = 0; i < 3; ++i) {
	if (!shownaxes[i]) continue;
	pair vertex = vertices[i];
	string axisname = axisnames[i];
	pair sum = 0;
	
	for (int j = 0; j < 3; ++j) {
		if (j == i) continue;
		sum += vertices[j];
	}
	
	sum /= 2;
	drawExtLine(vertex, sum, labelaxes ? axisname : "");
}
}

}

string _1 = "1";
string _2 = "2";
string _3 = "3";

string[][] orders = {
{_1, _2, _3},
{_1, _3, _2},
{_3, _2, _1},
{_2, _1, _3},
{_3, _1, _2},
{_2, _3, _1}
};

string[] names = {"$I$", "$A$", "$B$", "$C$", "$D$", "$E$"};

\end{asydef}

\begin{figure}[h]
\begin{minipage}{0.3\textwidth}
\begin{center}
\begin{asy}
size(0,80);

pair offset = (0,0);
string[] order = {"1", "2", "3"};

drawTriangle(offset, 2, order);
\end{asy}
\end{center}
\caption{The paper triangle.}
\label{paper_triangle}
\end{minipage}
%
\begin{minipage}{0.3\textwidth}
\begin{center}
\begin{asy}
size(0,80);

pair offset = (0,0);
string[] order = {"", "", ""};

drawTriangle(offset, 2, order, true);
\end{asy}
\end{center}
\caption{Its axes of reflection.}
\label{triangle_reflections}
\end{minipage}
\end{figure}

In the previous section, we started with the dihedral group of the equilateral triangle and discovered it had $6$ elements: reflections about three different axes, rotations of $\pm 120^{\circ}$, and the identity element. We identified a subgroup consisting of the identity $I$ with two rotations $r$ and $r^2$, and three other subgroups of just the identity and a single reflection.

\begin{enumerate}
\item Notice that the original dihedral group had twice as many elements as the rotation group. Why?
\item Make and justify a conjecture extending this observation to the dihedral groups of other shapes like rectangles, squares, hexagons, cubes, etc.
\item Write a table for the dihedral group of the rectangle, recalling that the allowed isometries are reflections and rotations. Let $r$ be a $180^{\circ}$ rotation, $x$ a reflection over the $x$-axis, and $y$ be a reflection over the $y$-axis. How does this table differ from the dihedral group of the equilateral triangle?
\item Write a table for the \textit{rotation group} of the square, with $4$ elements and $16$ entries. Compare this table to problem 3.
\end{enumerate}

We noticed that the rotation group for the equilateral triangle could be generated by just one of the elements, such as $r$---rotation by $120^{\circ}$ counterclockwise (or ccw). Then $r^2$ is a rotation of $240^{\circ}$ ccw, and $r^3=I$, the identity (see Figure ~\ref{successive_rotations}). Since we can generate the entire rotation group with a single element $r$, a natural question to ask is whether we can do the same with the dihedral group $D_3$. Clearly we can't use the identity to do it, and a series of rotations always leaves us with a rotation, never a reflection. Also, a series of flips along one axis simply generates a two member group with elements $I$, $f$ (see Figure ~\ref{flips}).

\begin{figure}
\begin{minipage}{0.5\textwidth}
\begin{center}
\begin{asy}
size(0,60);

drawTriangle((0, 0), 2, orders[4], false);
label("$r$", (sqrt(3) / 2, -1.4), (0,0), basealign);
drawTriangle((3, 0), 2, orders[5], false);
label("$r^2$", (3 + sqrt(3) / 2, -1.4), (0,0), basealign);
drawTriangle((6, 0), 2, orders[0], false);
label("$I$", (6 + sqrt(3) / 2, -1.4), (0,0), basealign);

label("$\stackrel{r}{\Longrightarrow}$", (2.4, 0.2));
label("$\stackrel{r}{\Longrightarrow}$", (5.4, 0.2));
label("$\stackrel{r}{\Longrightarrow}r$", (8.7, 0.2));
\end{asy}

\end{center}
\caption{${r}$ generates a three member group.}
\label{successive_rotations}
\end{minipage}%
\begin{minipage}{0.5\textwidth}
\begin{center}
\begin{asy}
size(0,60);

drawTriangle((0, 0), 2, orders[1], false);
label("$f$", (sqrt(3) / 2, -1.4), (0,0), basealign);
drawTriangle((3, 0), 2, orders[0], false);
label("$I$", (3 + sqrt(3) / 2, -1.4), (0,0), basealign);

label("$\stackrel{f}{\Longrightarrow}$", (2.4, 0.2));
label("$\stackrel{f}{\Longrightarrow}f$", (5.7, 0.1));
\end{asy}
\end{center}
\caption{${f}$ generates a two member group.}
\label{flips}
\end{minipage}
\end{figure}

Let's try using two elements to generate our group, using the same definitions of $f$ and $r$ as in the previous section: a flip over the $A$ axis and rotation by $120^{\circ}$ ccw, respectively. As we found, $fr$ is a flip over the $B$ axis and $rf$ is a flip over the $C$ axis. Consecutive powers of $r$ already got us the remaining elements, so ${r,f}$ generates the group.

We can also generate the group using two reflections, say $f$ and $f_B$ (flip over the $B$ axis, as shown in Figure ~\ref{triangle_reflections}). Notice that an even number of reflections always results in a rotation---even the identity element $I$ is just a rotation by $0$. We can think of this as the existence of a ``mirror world'' and its unmirrored counterpart, and each reflection takes us into or out of the mirror world. 
\end{document}