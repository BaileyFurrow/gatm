\documentclass[../gatm.tex]{subfiles}

\begin{document}

\section{From Snaps to Flips}

\newcommand{\degree}{\ensuremath{^\circ}}

\begin{asydef}
pair unitify(pair p) {
return p / (sqrt(p.x * p.x + p.y * p.y));
}

pair cis(real angle) {
return (cos(angle), sin(angle));
}

void drawTriangle(pair offset, real v_stretch, string[] order, bool showaxes = false, bool[] shownaxes = {true, true, true}, bool labelaxes = true) {
pair A = offset + (sqrt(3), 0);
pair B = offset + (0, 1);
pair C = offset + (0, -1);
pair O = offset + (sqrt(3) / 3, 0);

draw(A--B--C--cycle);

real egg = v_stretch - 1;

label(order[0], (O + egg * A) / v_stretch);
label(order[1], (O + egg * B) / v_stretch);
label(order[2], (O + egg * C) / v_stretch);

void drawExtLine(pair A, pair B, real extra = 0.2, string labelstr = "") {
	pair C = (A + extra * (A - B)), D = (B + extra * (B - A));
	draw(C -- D, dashed);
	label(labelstr, C, unitify(A - B));
}

if (showaxes) {
pair[] vertices = {A, B, C};
string[] axisnames = {"$A$", "$B$", "$C$"};

for (int i = 0; i < 3; ++i) {
	if (!shownaxes[i]) continue;
	pair vertex = vertices[i];
	string axisname = axisnames[i];
	pair sum = 0;
	
	for (int j = 0; j < 3; ++j) {
		if (j == i) continue;
		sum += vertices[j];
	}
	
	sum /= 2;
	drawExtLine(vertex, sum, labelaxes ? axisname : "");
}
}

}

string _1 = "1";
string _2 = "2";
string _3 = "3";

string[][] orders = {
{_1, _2, _3},
{_1, _3, _2},
{_3, _2, _1},
{_2, _1, _3},
{_3, _1, _2},
{_2, _3, _1}
};

string[] names = {"$I$", "$A$", "$B$", "$C$", "$D$", "$E$"};

\end{asydef}

\begin{figure}[h]
\begin{minipage}{0.3\textwidth}
\begin{center}
\begin{asy}
unitsize(1cm);

pair offset = (0,0);
string[] order = {"1", "2", "3"};

drawTriangle(offset, 2, order);
\end{asy}
\end{center}
\caption{The paper triangle.}
\label{paper_triangle}
\end{minipage}
\begin{minipage}{0.4\textwidth}
\begin{center}
\begin{asy}
unitsize(1cm);

pair offset = (0,0);
string[] order = {"", "", ""};

drawTriangle(offset, 2, order, true);
\end{asy}
\end{center}
\caption{The triangles axes of reflection.}
\label{triangle_reflections}
\end{minipage}
\end{figure}

\noindent Cut out a paper equilateral triangle, label its front vertices $1$, $2$, and $3$ in marker as shown in Figure ~\ref{paper_triangle}, and place it down on a table in the shown orientation. From this starting position, you can reflect the triangle over one of three axes --- A, B, or C --- or rotate the triangle 120\degree{} or 240\degree{} counterclockwise. The final possible positions are shown in Figure ~\ref{triangle_isos}.

\begin{figure}[h]

\begin{center}
\begin{asy}
unitsize(0.7cm);

drawTriangle((-4, 0), 2, orders[0], false);
label("$\stackrel{\mbox{\small flip!}}{\Longrightarrow}$", (-1, 0));

for (int i = 0; i < 6; ++i) {
	bool[] shownaxes = {false, false, false};
	
	if (1 <= i && i <= 3) {
		shownaxes[i - 1] = true;
	}
	
	if (i == 4 || i == 5) {
		real angle = (i == 4) ? 2 * pi / 3 : 4 * pi / 3;
		real radius = 0.8;
		pair center = (2.4 * i + sqrt(3) / 2, 1.7);
		pair end = (center + radius * cis(angle));
		
		draw((center + radius * cis(0)) .. (center + radius * cis(angle / 2)) .. end -- end, EndArrow);
		label((i == 4) ? "$\phantom{\degree}120\degree$" : "$\phantom{\degree}240\degree$", center);
	}
	
	drawTriangle((2.4 * i, 0), 2, orders[i], true, shownaxes, false);
	label(names[i], (2.4 * i + sqrt(3) / 2, -1.4));
}


\end{asy}
\end{center}
\caption{The six ending positions.}
\label{triangle_isos}
\end{figure}

Notice that each position corresponds to a different operation. For example, $I$ means ``leave the triangle alone,'' $A$ means ``flip the triangle about the A axis,'' and $D$ means ``rotate the triangle 120\degree{} counterclockwise.'' We can combine these operations to form other operations by writing them in sequence. Unlike most cases, however, we evaluate them right-to-left rather than left-to-right. For example, $AID=B$, as shown in Figure ~\ref{aid_is_b}.

Naturally, these six positions form another group, the \textbf{dihedral group} $D_3$, under the operation of combination. Splitting up ``dihedral'' into ``di-'' and ``-hedral,'' we see it means ``two faces.'' This refers to the two faces of our paper triangle!

1. 

\begin{figure}[h]
\begin{center}
\begin{asy}
unitsize(0.7cm);
drawTriangle((0, 0), 2, orders[4], false);
label(names[4], (sqrt(3) / 2, -1.4));
drawTriangle((3, 0), 2, orders[4], false);
drawTriangle((6, 0), 2, orders[2], false);
label(names[2], (6 + sqrt(3) / 2, -1.4));

label("$\stackrel{I}{\Longrightarrow}$", (2.4, 0.2));
label("$\stackrel{A}{\Longrightarrow}$", (5.4, 0.2));
\end{asy}
\end{center}
\caption{$AID = B$. Notice the right-to-left evaluation.}
\label{aid_is_b}
\end{figure}

\end{document}